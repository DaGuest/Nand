/** Implements the Dodge game */
class DodgeGame {
    field Background background;
    field Player player;

    /** Constructs a new DodgeGame. */
    constructor DodgeGame new() {
        let background = Background.new();
        let player = Player.new();
        return this;
    }

    /** Disposes this class */
    method void dispose() {
        do background.dispose();
        do player.dispose();
        do Memory.deAlloc(this);
        return;
    }

    /** Starts running the game loop. */
    method void run() {
        var char key;
        var boolean exit;
        let exit = false;

        while (~exit) {
            do player.draw();
            do background.scroll();
            
            let key = Keyboard.keyPressed();
            if (key = 113) {
                let exit = true;
            }
            else {
                // UP-arrow key pressed
                if (key = 131) {
                    do player.move(true);
                }
                else {
                    // DOWN-arrow key pressed
                    if (key = 133) {
                        do player.move(false);
                    }
                }
            }
            do Sys.wait(50);
        }
        return;
    }
}