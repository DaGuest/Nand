/** Implements the Dodge game */
class DodgeGame {
    field Background background;
    field Player player;
    field SplashScreen splashScreen;

    /** Constructs a new DodgeGame object. */
    constructor DodgeGame new() {
        let background = Background.new();
        let player = Player.new();
        let splashScreen = SplashScreen.new();
        return this;
    }

    /** Disposes this class */
    method void dispose() {
        do background.dispose();
        do player.dispose();
        do splashScreen.dispose();
        do Memory.deAlloc(this);
        return;
    }

    /************************************/
    //     BUSINESS LOGIC METHODS
    /************************************/

    /** Starts running the game loop. */
    method void run() {
        var char key;
        var int difficultyTimer; 
        var boolean exit;
        var boolean start;

        let exit = false;
        let start = false;
        let difficultyTimer = 64;

        // Draw a splashscreen with game isntructions.
        while (~start) {
            do splashScreen.drawSplashScreen("-- Dodge the walls! --", " ","Use up/down arrow keys.", "To start: hold SPACE!");
            let key = Keyboard.keyPressed();
            if (key = 32) {
                let start = true;
            }
            do Sys.wait(50);
        }
        do splashScreen.drawBaseRectangle();

        // Run the game loop and handle input.
        while (~exit) {
            let difficultyTimer = difficultyTimer - 1;
            if (difficultyTimer = 0) {
                do background.reduceGapSize();
                let difficultyTimer = 64;
            }

            do player.draw(false);
            do background.scroll();
            
            let key = Keyboard.keyPressed();
            if (key = 113) {
                let exit = true;
            }
            else {
                // UP-arrow key pressed
                if (key = 131) {
                    do player.move(true);
                }
                else {
                    // DOWN-arrow key pressed
                    if (key = 133) {
                        do player.move(false);
                    }
                }
            }
            let exit = background.checkCollision(player.getPositionY());
            do Sys.wait(50);
        }

        // Draw a game over splashscreen.
        do player.draw(true);
        do splashScreen.drawSplashScreen(" ", "Game Over!", " ", "Again? Please Reload");
        return;
    }
}